// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
    id "com.jfrog.bintray" version "1.7"
    id 'ch.netzwerg.release' version '1.2.4'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'checkstyle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

String projectVersion = project.version.toString()
group= "com.lousylynx.otfl"
archivesBaseName = "otfl"

//project.version.with { major = 2; minor= 3; patch = 4; preRelease = "beta1"; releaseBuild = true }
minecraft {
    version = "1.10.2-12.18.3.2185"
    runDir = "run"
    mappings = "stable_29"
}

dependencies {

}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", projectVersion
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':projectVersion, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    include "com/lousylynx/otfl/**"
    include "assets/**"
    include "mcmod.info"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

task apiJar(type: Jar, dependsOn: 'sourceMainJava') {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'com/lousylynx/otfl/api/**/*'
    include 'com/lousylynx/otfl/library/**/*'
    include 'com/lousylynx/otfl/OnTheFly'
    classifier 'api'
}

artifacts {
    archives deobfJar
    archives apiJar
    archives sourceJar
    archives jar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = projectVersion + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            artifact deobfJar
            artifact sourceJar
            artifact apiJar
            artifact jar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']

    if(user != null) {
        publish = true
    } else {
        publish = false
    }

    pkg {
        repo = 'dev'
        name = 'On-The-Fly'
        version {
            name = projectVersion + '-' + System.getenv('TRAVIS_BUILD_NUMBER')
            released = new Date()
        }
    }
}

release {
    dependsOn build

    if(System.getenv('BINTRAY_USER') != null){
        push = true
    }

    versionSuffix = '-SNAPSHOT'
    tagPrefix = 'v'
}